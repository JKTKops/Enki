do not use base.

list may be
    empty.
    cons Head Tail has
        Head : int,
        Tail : list.

length of List is
    when List = cons H T then 1 + length of T.
    otherwise then 0.

construct Head Full is
    Full = (cons Head Tail),
    Tail.

concat A B is
    when A = cons H T then cons H (concat T B).
    otherwise then B.

reverseAcc A Acc is
    when A = cons H T then reverseAcc T (cons H Acc).
    otherwise then Acc.

reverse list A is reverseAcc A empty.

range Low to High is
    when Low > High then empty.
    otherwise then cons Low (range (Low + 1) to High).

sum List is N where
    when List = cons H T then sum T is (N - H).
    otherwise then N = 0.

formatHelper List is
    when List = cons H empty then H .. "]".
    when List = cons H T then H .. "," .. formatHelper T.
    otherwise then "]".

format list List is "[" .. formatHelper List.

display (format list (range 1 to 1000)).

